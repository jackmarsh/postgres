def postgres(name:str, version:str, os=None, arch=None, hashes:list=[], visibility:list=[], configure_flags:list=[], _compress:bool=False):
    """
    Builds PostgreSQL binaries from source for a specific OS and architecture.

    Args:
        name (str): Name of the rule.
        version (str): PostgreSQL version to build.
        os (str): Target operating system. Defaults to the current OS.
        arch (str): Target architecture. Defaults to the current architecture.
        hashes (list): Optional hashes to verify the download.
        visibility (list): Visibility declaration for the rule.
        configure_flags (list): Additional flags for PostgreSQL's configure script.
    """
    if not os:
        if CONFIG.OS == 'linux':
            os = 'linux'
        elif CONFIG.OS == 'darwin':
            os = 'osx'
        else:
            raise ParseError(f'Unknown OS {CONFIG.OS}, you must pass explitly')
    if not arch:
        if CONFIG.ARCH == 'amd64':
            arch = 'x86_64'
        elif CONFIG.ARCH == 'x86':
            arch = 'i686'
        else:
            raise ParseError(f'Unknown architecture {CONFIG.ARCH}, you must pass explitly')
        
    target = f'{os}_{arch}'
    
    # Download the PostgreSQL source.
    url = f"https://ftp.postgresql.org/pub/source/v{version}/postgresql-{version}.tar.gz"
    download = remote_file(
        name=tag(name, f"download_{target}"),
        url=url,
        hashes=hashes,
    )
    # 
    extract = build_rule(
        name=name,
        tag=f'extract_{target}',
        srcs=[download],
        outs=[f'postgresql-{version}-{target}'],
        cmd = ' && '.join([
            'tar -xzf $SRC',
            f'mv postgresql-{version} $OUT'
        ]),
        building_description = f'Extracting PostgreSQL v{version} for {target}...',
    )
    install = build_rule(
        # Don't tag, --prefix doesn't recognise '#'.
        name=f'_{name}_{target}_install', 
        srcs=[extract],
        outs = {
            "bin": [f'{target}/psql/bin'],
            "lib": [f'{target}/psql/lib'],
            "include": [f'{target}/psql/include'],
            "share": [f'{target}/psql/share'],
        },
        cmd = ' && '.join([
            f'mkdir -p {target}/psql',
            'cd $SRC',
            f'./configure --prefix "$HOME/{target}/psql" ' + ' '.join(configure_flags),
            'make',
            'make install',
            'cd $HOME',
        ]),
        building_description = f'Installing PostgreSQL v{version} for {target}...',
    )
    if _compress:
        tarball(
            name=name,
            out=f'psql-{version}-{target}.tar.gz',
            srcs=[
                install + '|bin',
                install + '|lib',
                install + '|include',
                install + '|share',
            ],
        )
    else:
        filegroup(
            name=name,
            srcs=[
                install + '|bin',
                install + '|lib',
                install + '|include',
                install + '|share',
            ],
            deps = [install],
            binary=True,
            visibility=visibility,
        )
